apply from: '../gradle/base-information.gradle'

apply plugin: 'distribution'
apply plugin: 'com.jfrog.bintray'


configurations {
	bundles {
		description = 'Configuration used to group the archives output from the distribution plugin.'
	}
}

evaluationDependsOn( ':hibernate-core' )
evaluationDependsOn( ':hibernate-c3p0' )


distributions {
	main {
		baseName = 'hibernate-release'
		contents {
			into('lib/required') {
				from rootProject.project( 'hibernate-core' ).configurations.provided.files { dep -> dep.name == 'jta' }
				from rootProject.project( 'hibernate-core' ).configurations.runtime
				from rootProject.project( 'hibernate-core' ).configurations.archives.allArtifacts.files.filter{ file -> !file.name.endsWith('-sources.jar') }
				// for now,
				from rootProject.project( 'hibernate-core' ).configurations.provided.files { dep -> dep.name == 'javassist' }
			}

			into( 'lib/c3p0' ) {
				from (
						( rootProject.project( 'hibernate-c3p0' ).configurations.archives.allArtifacts.files.filter{ file -> !file.name.endsWith('-sources.jar') }
								+ rootProject.project( 'hibernate-c3p0' ).configurations.runtime )
								- rootProject.project( 'hibernate-core' ).configurations.runtime
								- rootProject.project( 'hibernate-core' ).configurations.archives.allArtifacts.files
				)
			}
		}
	}
}

distTar {
	compression = Compression.GZIP
}

artifacts {
	bundles distTar
	bundles distZip
}

bintray {
	user = project.property( 'PERSONAL_BINTRAY_USER' )
	key = project.property( 'PERSONAL_BINTRAY_API_KEY' )

	configurations = ['bundles']

	pkg {
		userOrg = user
		repo = 'generic'
		name = 'hibernate-orm-gradle-clean-room'
		version {
			name = version
			gpg {
				sign = true
			}
			attributes = [
					'jpa': '2.2',
					'family': project.hibernateMajorMinorVersion
			]
		}
	}

	publish = false
}

if ( version.toString().endsWith( '-SNAPSHOT' ) ) {
	tasks.bintrayUpload {
		enabled = false

		doFirst {
			throw new GradleException( 'Cannot publish release bundles for SNAPSHOT versions' )
		}
	}
}